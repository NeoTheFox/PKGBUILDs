# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: TZ86
# Contributer: NeoTheFox <alarm at reprator.33mail.com>

pkgname=vivaldi
_debversion=4.2.2406.54-1
pkgver=4.2.2406.54
pkgrel=1
pkgdesc='An advanced browser made with the power user in mind.'
url="https://vivaldi.com"
options=(!strip !zipman)
license=('custom')
arch=('aarch64' 'armv7h')
depends=('gtk3' 'libcups' 'nss' 'alsa-lib' 'libxss' 'ttf-font' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('w3m')
optdepends=(
    'vivaldi-ffmpeg-codecs: playback of proprietary video/audio'
    'libnotify: native notifications'
)
source_aarch64=("https://downloads.vivaldi.com/stable/vivaldi-stable_${_debversion}_arm64.deb")
source_armv7h=("https://downloads.vivaldi.com/stable/vivaldi-stable_${_debversion}_armhf.deb")
sha512sums_aarch64=('dfb80f16d6369b7bd4f7d665fee0abfc888cd4e80afdfa5978cf21364bc04814d94507fab4e32459122b719705208f1181a45056ba9f92e3c1117d514f597896')
sha512sums_armv7h=('df1566f03fef7baa31c299d8f043fb2f2f0f68772180fd197a4c2fe9445cda023416972921420644313e725fdb7685b8713cf24efed421418e869c5f6b1a85ef')

package() {
    tar -xf data.tar.xz -C "$srcdir"
    cp --parents -a {opt,usr/bin,usr/share} "$pkgdir"

    # suid sandbox
    chmod 4755 "$pkgdir/opt/$pkgname/vivaldi-sandbox"

    # make /usr/bin/vivaldi-stable available
    binf="$pkgdir/usr/bin/vivaldi-stable"
    if [[ ! -e "$binf" ]] && [[ ! -f "$binf" ]] && [[ ! -L "$binf" ]]; then
        install -dm755 "$pkgdir/usr/bin"
        ln -s /opt/vivaldi/vivaldi "$binf"
    fi

    # install icons
    for res in 16 22 24 32 48 64 128 256; do
        install -Dm644 "$pkgdir/opt/$pkgname/product_logo_${res}.png" \
            "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/$pkgname.png"
    done

    # license
    install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
    w3m -dump "$pkgdir/opt/$pkgname/LICENSE.html" \
        | head -n 5 \
        > "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

